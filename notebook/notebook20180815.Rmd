---
title: "notebook20180815"
author: "Mark Hagemann"
date: "August 15, 2018"
output: html_document
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = TRUE)
```

I'm working on consolidating the bamr stan files into a single file. This requires some hacking, as suggested [here](https://github.com/stan-dev/stan/issues/2377). The issue I'm having currently is how to give default "null" inputs, e.g. when running AMHG-only. Do these need to be matrices with one zero-length dimension and one nonzer-length dimension (e.g. `matrix(nrow = 0, ncol = 5))`)? Or can I specify as a truly zero-sized array? I'd prefer the latter. Let's find out. 

```{stan output.var = "stantest"}
data {
  int<lower=0> nx;
  int<lower=0> nt;
  int<lower=0, upper=1> inc_y;
  
  vector[nt] x[nx];
  vector[nt * inc_y] y[nx * inc_y];
  
  real<lower=0> sigma_x;
  real<lower=0> sigma_y[inc_y];
}

parameters {
  vector[nx] mu_x;
  vector[nx] mu_y[inc_y];
}

model {
  for (i in 1:nx) {
    x[i] ~ normal(mu_x[i], sigma_x);
    if (inc_y == 1) {
      y[i] ~ normal(mu_y[1][i], sigma_y[1]);
    }
  }
  mu_x ~ normal(1, 10);
  if (inc_y)
    mu_y[1] ~ normal(1, 10);
}
```

```{r}
library(rstan)
nx <- 10
nt <- 20
testdata <- list(
  nx = nx,
  nt = nt,
  # inc_y = 1,
  inc_y = 0,
  x = matrix(rnorm(nx * nt), nr = nx, nc = nt),
  # y = matrix(rnorm(nx * nt), nr = nx, nc = nt),
  y = array(dim = c(0, 10)),
  sigma_x = 1,
  # sigma_y = array(1, dim = 1)
  sigma_y = numeric(0)
)

est1 <- sampling(stantest, data = testdata, cores = 2, chains = 2)
```

Some notes as I wait for the model to compile. Stan array dimensions, particularly when one of the dimensions is zero, do not appear to play nice with R dimensions. In particular I'm having a hard time feeding in a data object with dimensions (0, nt). Now trying when dimensions are (0, 0). 

I'm concluding that specifying zero-dimension data is not feasible. But I don't have to do it either. I can just put dummy data with nonzero dimensionality and have zero-dimension parameters. That's easy:

```{stan output.var = "stantest2"}
data {
  int<lower=0> nx;
  int<lower=0> nt;
  int<lower=0, upper=1> inc_y;
  
  vector[nt] x[nx];
  vector[(nt - 1) * inc_y + 1] y[(nx - 1) * inc_y + 1];
  
  real<lower=0> sigma_x;
  real<lower=0> sigma_y;
}

parameters {
  vector[nx] mu_x;
  vector[nx] mu_y[inc_y];
}

model {
  for (i in 1:nx) {
    x[i] ~ normal(mu_x[i], sigma_x);
    if (inc_y == 1) {
      y[i] ~ normal(mu_y[1][i], sigma_y);
    }
  }
  mu_x ~ normal(1, 10);
  if (inc_y)
    mu_y[1] ~ normal(1, 10);
}
```

This should work:

```{r}
nx <- 10
nt <- 20
testdata1 <- list(
  nx = nx,
  nt = nt,
  inc_y = 1,
  x = matrix(rnorm(nx * nt), nr = nx, nc = nt),
  y = matrix(rnorm(nx * nt), nr = nx, nc = nt),
  sigma_x = 1,
  sigma_y = 1
)

testdata2 <- within(testdata1, {
  inc_y <- 0
  y <- matrix(1)
})

est1 <- sampling(stantest2, data = testdata1, cores = 2, chains = 2)
est2 <- sampling(stantest2, data = testdata2, cores = 2, chains = 2)

summary(est1)$summary
summary(est2)$summary


```

Beautiful. I like this!


On another note, I need to figure out how to treat certain transformed parameters. I don't want to have to store a bunch of logW matrices when this is only sometimes a parameter (when meas_err is TRUE) but otherwise an observed value. Options seem to be:

- Make the transformed parameter as a full-sized matrix always, just don't store it (include = FALSE). Internally this doesn't change anything (I think), just reduces the size of the returned object. 
- Make more involved tree logic to differentiate between the meas_err case and otherwise. This would look messier, but would allow logW_act or whatever I end up calling it to be a zero-length array if meas_err is FALSE. 

I like the second way. This would look like:

```{stan}
transformed parameters {

  vector[nt] man_lhs[nx * inc_m];
  vector[nt] logA_man[nx * inc_m]; // log area for Manning's equation
  vector[nt] man_rhs[nx * inc_m]; // RHS for Manning likelihood
  vector[nt] amhg_rhs[nx * inc_a]; // RHS for AMHG likelihood
  
  for (i in 1:nx) {
    if (inc_m) {
      logA_man[i] = log(A0[1][i] + (meas_err ? dAact[i] : dA_pos[i]));
      man_rhs[i] = 10. * logA_man[i] - 6. * logn[1] - 6. * logQ;
      if (meas_err)
        man_lhs[i] = 4. * log(Wact[i]) - 3. * log(Sact[i]); // LHS of manning equation
      else
        man_lhs[i] = 4. * log(Wobs[i]) - 3. * log(Sobs[i]); // LHS of manning equation
    }
    if (inc_a) {
      amhg_rhs[i] = b[1][i] * (logQ - logQc[1]) + logWc[1];
    }
  }
}

model {

  // Priors
  logQ ~ normal(logQ_hat, logQ_sd);
  
  if (inc_m) {
    A0[1] + dA_shift[1] ~ lognormal(logA0_hat, logA0_sd);
    logn[1] ~ normal(logn_hat, logn_sd);
  }
  if (inc_a) {
    b[1] ~ normal(b_hat, b_sd);
    logWc ~ normal(logWc_hat, logWc_sd);
    logQc ~ normal(logQc_hat, logQc_sd);
  }
  
  // Likelihood and observation error
  for (i in 1:nx) {
    if (inc_m) {
      man_lhs[i] ~ normal(man_rhs[i], 6 * sigma_man[i]);
    }
    
    if (meas_err) {
      Wact[i] ~ normal(Wobs[i], Werr_sd);
      
      if (inc_m) {
        Sact[i] ~ normal(Sobs[i], Serr_sd);
        dAact[i] ~ normal(dA_pos[i], dAerr_sd);
        target += -logW[i];
        target += -logS[i];      
      }
      if (inc_a) {
        Wact[i] ~ lognormal(amhg_rhs[i], sigma_amhg[i]);
      }
    }
    else {
      if (inc_a) {
        Wobs[i] ~ lognormal(amhg_rhs[i], sigma_amhg[i]);
      }
    }
  }
}
```

OK, I put that into the master.stan file. It looks pretty ugly. Also note that I did not adhere to my no-superfluous-transformed-params rule, as I've left man_lhs as a transformed param even though it should be transformed data when meas_err is FALSE. I may revisit in the future. First though, let's test this one out outside of a bamr package compilation. 

```{r}
inps <- bamr:::compose_bam_inputs(swot_bamdata(reachdata$Ganges))
inps$inc_m <- 1
inps$inc_a <- 0
inps$meas_err <- 0
inps$dA_shift <- list(inps$dA_shift)

mergemod <- stan_model("../../bamr/exec/master.stan")

opts1 <- optimizing(mergemod, data = inps, as_vector = FALSE)
opts1$par$A0
opts1$par$b

inps2 <- within(inps, {
  inc_a <- TRUE
})
samps1 <- sampling(mergemod, data = inps, cores = 2, chains = 2, iter = 1000)
stan_trace(samps1, pars = "A0")

opts2 <- optimizing(mergemod, data = inps2, as_vector = FALSE)
opts2$par$A0
opts2$par$b

samps2 <- sampling(mergemod, data = inps2, cores = 2, chains = 2, iter = 1000)
stan_trace(samps2, pars = "A0")
stan_trace(samps2, pars = "b")
```

This is looking good so far. More testing is required, but I think I can move on to fixing the dataprep with a single stanfile in mind. 